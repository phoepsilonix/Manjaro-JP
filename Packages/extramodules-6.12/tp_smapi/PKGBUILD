# Maintainer: Philip Müller <philm[at]manjaro[dot]org>
# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

_linuxprefix=linux612

_module=tp_smapi
pkgname="${_linuxprefix}-${_module}"
pkgver=0.44
pkgrel=16
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
groups=("${_linuxprefix}-extramodules")
depends=("${_linuxprefix}")
makedepends=('git' "${_linuxprefix}-headers")
provides=("${_module}")
_commit=6e80bb1752280bcd142d86ecd0739661bd0e8312  # tags/tp-smapi/0.44^0
source=("git+https://github.com/evgeni/tp_smapi#commit=$_commit"
        'tp_smapi-clang.patch')
sha256sums=('166892e1ac453f7284c1defbad0e89f67f09cae9859c2b0629a168c6a5060d53'
            'cb5d682450af8ac8436e1645d4fdaa32382ae83cc03c03b0c2f878f78e017be5')

prepare() {
  cd "${_module}"
  patch -p1 -i ../tp_smapi-clang.patch
}

pkgver() {
  cd "${_module}"
  git describe --tags | sed 's/^tp-smapi\///;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"

  cd "${_module}"

  # https://bugs.archlinux.org/task/54975 (kernel has no _GLOBAL_OFFSET_TABLE_):
  # Clear EXTRA_CFLAGS since it defaults to injecting CFLAGS and -fno-plt breaks the modules
  make HDAPS=1 KVER="${_kernver}" EXTRA_CFLAGS=
}

package() {
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"

  cd "${_module}"
  SIG_HASH=$(grep -o "CONFIG_MODULE_SIG_HASH.*" /usr/lib/modules/$_kernver/build/include/config/auto.conf)
  SIG_HASH=${SIG_HASH#CONFIG_MODULE_SIG_HASH=}
  /usr/lib/modules/${_kernver}/build/scripts/sign-file $SIG_HASH /var/lib/dkms/signing_key-${_kernver}.pem /var/lib/dkms/signing_key-${_kernver}.x509 ${_module}.ko
  cd ..
  find . -name "*.ko" -exec install -Dt "$pkgdir/usr/lib/modules/${_kernver}/extramodules" {} +
  COMPRESS=$(grep -o "CONFIG_MODULE_COMPRESS_.* 1" /usr/lib/modules/$_kernver/build/include/generated/autoconf.h|grep -v ALL)
  COMPRESS=${COMPRESS#CONFIG_MODULE_COMPRESS_}
  COMPRESS=${COMPRESS% 1}
  case $COMPRESS in
  ZSTD)
    COMP_CMD="zstd -T0 -22 --ultra --auto-threads=physical --rm -f"
    ;;
  XZ)
    COMP_CMD="xz -T0 -f -v -9 --lzma2=dict=2MiB"
    ;;
  esac
  find "$pkgdir" -name "*.ko" -exec strip --strip-debug {} +
  find "${pkgdir}" -name '*.ko' -exec $COMP_CMD {} +

  # load module on startup
  echo tp_smapi | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

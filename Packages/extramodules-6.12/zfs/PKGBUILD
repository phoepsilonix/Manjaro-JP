# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>

_linuxprefix=linux612

pkgname="${_linuxprefix}-zfs"
pkgver=2.2.7
pkgrel=8
pkgdesc='Kernel modules for the Zettabyte File System.'
arch=('x86_64')
url="http://zfsonlinux.org/"
license=('CDDL-1.0')
groups=("${_linuxprefix}-extramodules")
depends=("${_linuxprefix}" "zfs-utils=${pkgver}")
makedepends=("${_linuxprefix}-headers" "zfs-dkms=${pkgver}")
provides=("zfs=${pkgver}" "ZFS-MODULE=${pkgver}")
options=('!strip')

build() {
    _kernver="$(cat /usr/src/${_linuxprefix}/version)"

    fakeroot dkms build --dkmstree "${srcdir}" -m zfs/${pkgver} -k ${_kernver}
}

package() {
    _kernver="$(cat /usr/src/${_linuxprefix}/version)"

    install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 zfs/${pkgver}/${_kernver}/${CARCH}/module/*
    SIG_HASH=$(grep -o "CONFIG_MODULE_SIG_HASH.*" /usr/lib/modules/$_kernver/build/include/config/auto.conf)
    SIG_HASH=${SIG_HASH#CONFIG_MODULE_SIG_HASH=}
    files=$(find ${pkgdir} -type f -name '*.ko.*')
    for f in $files
    do
          case "$f" in
          *.zst)
                zstd -d $f
                COMPRESS=zstd
                ;;
          *.xz)
                xz -d $f
                COMPRESS=xz
                ;;
          esac
    done
    for file in $(find "${pkgdir}" -type f -name '*.ko')
    do
        /usr/lib/modules/${_kernver}/build/scripts/sign-file $SIG_HASH /var/lib/dkms/signing_key-${_kernver}.pem /var/lib/dkms/signing_key-${_kernver}.x509 $file
    done

    # compress each module individually
    COMPRESS=$(grep -o "CONFIG_MODULE_COMPRESS_.* 1" /usr/lib/modules/$_kernver/build/include/generated/autoconf.h|grep -v ALL)
    COMPRESS=${COMPRESS#CONFIG_MODULE_COMPRESS_}
    COMPRESS=${COMPRESS% 1}
    case $COMPRESS in
    ZSTD)
        COMP_CMD="zstd -T0 -22 --ultra --auto-threads=physical --rm -f"
        ;;
    XZ)
        COMP_CMD="xz -T0 -f -v -9 --lzma2=dict=2MiB"
        ;;
    esac
    find "${pkgdir}" -name '*.ko' -exec $COMP_CMD {} +

    # systemd module loading
    printf '%s\n' spl zfs |
    install -Dm 644 /dev/stdin "${pkgdir}/usr/lib/modules-load.d/${pkgname}.conf"
}

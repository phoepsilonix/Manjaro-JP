# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

_linuxprefix=linux612

_module=rtl8723bu
pkgname="${_linuxprefix}-${_module}"
pkgver=20240303
pkgrel=15
pkgdesc="Driver for RTL8723BU"
arch=('x86_64')
url="https://github.com/lwfinger/rtl8723bu"
license=('GPL')
groups=("${_linuxprefix}-extramodules")
depends=("${_linuxprefix}")
makedepends=('git' "${_linuxprefix}-headers")
provides=("${_module}")
_commit=af3a408d6399655b0db23c2c8720436ca725ca47  # master
source=("git+https://github.com/lwfinger/rtl8723bu.git#commit=${_commit}"
        'blacklist-rtl8xxxu.conf'
         static.patch)
sha256sums=('a9ff148d393079de8618db758905d42e48f098e6c7ebd5b12518f4e4a1117da2'
            '7c726ad04083c8e620bc11c837e5f51d3e9e2a5c3e19c333b2968eb39f1ef07e'
            '24b1e36cc3a26e9cc0050e0d51078b26a9e19176bc7cbfcee553d61bc9c38d71')

pkgver() {
    cd "${_module}"
    git show -s --date=format:'%Y%m%d' --format=%cd
}

prepare() {
    cd "${_module}"

    # do not compile with CONCURRENT_MODE
    sed -i 's/EXTRA_CFLAGS += -DCONFIG_CONCURRENT_MODE/#EXTRA_CFLAGS += -DCONFIG_CONCURRENT_MODE/g' Makefile
    patch -p1 -i ../static.patch
}

build() {
    _kernver="$(cat /usr/src/${_linuxprefix}/version)"

     cd "${_module}"

    # avoid using the Makefile directly -- it doesn't understand
    # any kernel but the current.
    make clean
    make -C /usr/lib/modules/${_kernver}/build M="$srcdir/${_module}" modules
    SIG_HASH=$(grep -o "CONFIG_MODULE_SIG_HASH.*" /usr/lib/modules/$_kernver/build/include/config/auto.conf)
    SIG_HASH=${SIG_HASH#CONFIG_MODULE_SIG_HASH=}
    /usr/lib/modules/${_kernver}/build/scripts/sign-file $SIG_HASH /var/lib/dkms/signing_key-${_kernver}.pem /var/lib/dkms/signing_key-${_kernver}.x509 *.ko
}

package() {
    _kernver="$(cat /usr/src/${_linuxprefix}/version)"

    install -Dm644 "blacklist-rtl8xxxu.conf" "${pkgdir}/usr/lib/modprobe.d/${_linuxprefix}-blacklist-rtl8xxxu.conf"
    install -Dm644 "${_module}"/*.ko -t "$pkgdir/usr/lib/modules/${_kernver}/extramodules/"
    COMPRESS=$(grep -o "CONFIG_MODULE_COMPRESS_.* 1" /usr/lib/modules/$_kernver/build/include/generated/autoconf.h|grep -v ALL)
    COMPRESS=${COMPRESS#CONFIG_MODULE_COMPRESS_}
    COMPRESS=${COMPRESS% 1}
    case $COMPRESS in
    ZSTD)
      COMP_CMD="zstd -T0 -22 --ultra --auto-threads=physical --rm -f"
      ;;
    XZ)
      COMP_CMD="xz -T0 -f -v -9 --lzma2=dict=2MiB"
      ;;
    esac
    find "${pkgdir}" -name '*.ko' -exec $COMP_CMD {} +
}

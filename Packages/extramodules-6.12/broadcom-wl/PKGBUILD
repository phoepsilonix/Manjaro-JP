# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_linuxprefix=linux612

_module=broadcom-wl
pkgname="${_linuxprefix}-${_module}"
pkgver=6.30.223.271
pkgrel=16
pkgdesc='Broadcom 802.11 Linux STA wireless driver'
arch=('x86_64')
url='https://www.broadcom.com/support/download-search/?pf=Wireless+LAN+Infrastructure'
license=('custom')
groups=("${_linuxprefix}-extramodules")
depends=("${_linuxprefix}")
makedepends=("${_module}-dkms=$pkgver" "${_linuxprefix}-headers")
provides=("${_module}")

build() {
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"

  fakeroot dkms build --dkmstree "$srcdir" -m "${_module}/$pkgver" -k "${_kernver}"
  cd "$srcdir/$_module/$pkgver/$_kernver/$CARCH/module"
  if [ -f *.ko.zst ] ;then
          zstd --rm -q -d *.zst
  elif [ -f *.ko.xz ] ;then
          xz -q -d *.xz
  fi
  SIG_HASH=$(grep -o "CONFIG_MODULE_SIG_HASH.*" /usr/lib/modules/$_kernver/build/include/config/auto.conf)
  SIG_HASH=${SIG_HASH#CONFIG_MODULE_SIG_HASH=}
  /usr/lib/modules/${_kernver}/build/scripts/sign-file $SIG_HASH /var/lib/dkms/signing_key-${_kernver}.pem /var/lib/dkms/signing_key-${_kernver}.x509 *.ko
}

package(){
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"

  install -Dm644 -t "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" \
    ${_module}/${pkgver}/${_kernver}/${CARCH}/module/*

  # compress kernel modules
  COMPRESS=$(grep -o "CONFIG_MODULE_COMPRESS_.* 1" /usr/lib/modules/$_kernver/build/include/generated/autoconf.h|grep -v ALL)
  COMPRESS=${COMPRESS#CONFIG_MODULE_COMPRESS_}
  COMPRESS=${COMPRESS% 1}
  case $COMPRESS in
  ZSTD)
    COMP_CMD="zstd -T0 -22 --ultra --auto-threads=physical --rm -f"
    ;;
  XZ)
    COMP_CMD="xz -T0 -f -v -9 --lzma2=dict=2MiB"
    ;;
  esac
  find "${pkgdir}" -name '*.ko' -exec $COMP_CMD {} +

  install -Dm644 "/usr/share/licenses/${_module}-dkms"/* -t \
    "${pkgdir}/usr/share/licenses/$pkgname/"
  install -Dm644 "/usr/lib/modprobe.d/${_module}-dkms.conf" \
      "${pkgdir}/usr/lib/modprobe.d/$pkgname.conf"
}

# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Maintainer: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: mortzu <me@mortzu.de>
# Contributor: fnord0 <fnord0@riseup.net>

_linuxprefix=linux612

_module=acpi_call
pkgname="${_linuxprefix}-${_module}"
pkgver=1.2.2
pkgrel=15
pkgdesc='A linux kernel module that enables calls to ACPI methods through /proc/acpi/call'
arch=('x86_64')
url="https://github.com/nix-community/acpi_call"
license=('GPL')
groups=("${_linuxprefix}-extramodules")
depends=("${_linuxprefix}" 'perl')
makedepends=("${_linuxprefix}-headers" "acpi_call-dkms=$pkgver")
provides=("${_module}")
conflicts=("${_module}-dkms")

build() {
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"

  fakeroot dkms build --dkmstree "$srcdir" -m "${_module}/$pkgver" -k "${_kernver}"
  cd "$srcdir/$_module/$pkgver/$_kernver/$CARCH/module"
  if [ -f *.ko.zst ] ;then
          zstd --rm -q -d *.zst
  elif [ -f *.ko.xz ] ;then
          xz -q -d *.xz
  fi
  SIG_HASH=$(grep -o "CONFIG_MODULE_SIG_HASH.*" /usr/lib/modules/$_kernver/build/include/config/auto.conf)
  SIG_HASH=${SIG_HASH#CONFIG_MODULE_SIG_HASH=}
  /usr/lib/modules/${_kernver}/build/scripts/sign-file $SIG_HASH /var/lib/dkms/signing_key-${_kernver}.pem /var/lib/dkms/signing_key-${_kernver}.x509 $_module.*
}

package() {
  _kernver="$(cat /usr/src/${_linuxprefix}/version)"


  ( cd "${_module}/${pkgver}/${_kernver}/$CARCH/module"; install -Dm644 *.ko* -t "$pkgdir/usr/lib/modules/${_kernver}/extramodules/" )

  # compress each module individually
  COMPRESS=$(grep -o "CONFIG_MODULE_COMPRESS_.* 1" /usr/lib/modules/$_kernver/build/include/generated/autoconf.h|grep -v ALL)
  COMPRESS=${COMPRESS#CONFIG_MODULE_COMPRESS_}
  COMPRESS=${COMPRESS% 1}
  case $COMPRESS in
  ZSTD)
    COMP_CMD="zstd -T0 -22 --ultra --auto-threads=physical --rm -f"
    ;;
  XZ)
    COMP_CMD="xz -T0 -f -v -9 --lzma2=dict=2MiB"
    ;;
  esac
  find "${pkgdir}" -name '*.ko' -exec $COMP_CMD {} +
  sync

  echo acpi_call | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"

  install -d "$pkgdir/usr/share"
  cp -a "/usr/share/${_module}" "$pkgdir/usr/share/$pkgname"
}
